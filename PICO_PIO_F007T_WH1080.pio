
.program PWMpulseBits
; divider 625.0F => sm clock period 5 us
; => short_timer 5*(2+1)=15 us; loop*16 = 240 us
; => long_timer  5*(3+1)=20 us; loop*30 = 600 us
; => gap_timer   5*(1+3)=20 us; loop*30 = 600 us

.wrap_target
start_of_hi:
    wait 1 pin 0
already_hi:
    set y, 15
short_timer:
    jmp y-- test_short_hi
    set y, 31
long_timer:
    jmp y-- test_long_hi
    set x, 0
    wait 0 pin 0

gap_start:    
    set y, 28
gap_timer:
    jmp pin already_hi
    jmp y-- gap_timer [2]
    in x, 1    
    jmp start_of_hi

test_short_hi:
    jmp pin short_timer [1]
.wrap

test_long_hi:
    jmp pin long_timer [2]
    set x, 1
    jmp gap_start

.program manchWithDelay
; 1 data bit @ 1024 kHz => 976.56 us,  
; with 125 MHz clock and dividor = 2543.0F
; giving 48 cycles/bit => 1 cycle time = 20.34 us of resolution
; short wait=12 => 244.08 us, long wait=24 => 488.16 us
; giving long test at 36 => 732.24 us

start_of_1:
    wait 0 pin 0 [9]     ; 10 short wait
    jmp  pin start_of_1  ; 11
    in   x, 1 [23]       ; 35 long wait
    jmp pin start_of_1   ; 36 => 48-36=12
.wrap_target
start_of_0:
    wait 1 pin 0 [9]     ; 10 short wait
    jmp pin emit_0       ; 11
    jmp start_of_0

emit_0:    
    in null, 1 [23]      ; 35 long wait
    jmp pin start_of_1   ; 36 => 48-36=12
.wrap
